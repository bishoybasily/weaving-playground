plugins {
    id 'io.freefair.aspectj.post-compile-weaving'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/aspect']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java', 'src/aspect']
        }
    }
}

configurations {
    aspectjWeaver
    weavingImplementation.extendsFrom implementation
}

dependencies {
    implementation libs.aspectjrt
    compileOnly libs.aspectjweaver
    aspectjWeaver libs.aspectjweaver
}

def COMPILER_EXTRA_ARGS = [
//        "-showWeaveInfo",
//        "-verbose",
        "-Xlint:adviceDidNotMatch=error"
]

tasks.named('compileJava') {
    ajc.options.compilerArgs
            += COMPILER_EXTRA_ARGS
}

tasks.named('compileTestJava') {
    ajc.options.compilerArgs
            += COMPILER_EXTRA_ARGS
}

afterEvaluate {

    configurations.weavingImplementation.dependencies.forEach {

        var module = project(":" + it.name)
        var moduleBootJarTask = module.tasks.named("bootJar")

        def COMPILER_INPATH_ARG = [
                "-inpath", moduleBootJarTask.get().archiveFile.get().asFile.absolutePath
        ]

        tasks.named('compileJava') {
            dependsOn(moduleBootJarTask)
            ajc.options.compilerArgs
                    += COMPILER_INPATH_ARG
        }

        tasks.named('compileTestJava') {
            dependsOn(moduleBootJarTask)
            ajc.options.compilerArgs
                    += COMPILER_INPATH_ARG
        }

    }

}
