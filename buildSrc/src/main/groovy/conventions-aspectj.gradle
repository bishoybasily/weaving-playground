plugins {
    id 'io.freefair.aspectj.post-compile-weaving'
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    'src/main/aspect'
            ]
        }
    }
    test {
        java {
            srcDirs = [
                    'src/main/java', 'src/test/java',
                    'src/main/aspect', 'src/test/java'
            ]
        }
    }
}

configurations {
    aspectjWeaver
    weavingCompleteImplementation

    implementation.extendsFrom weavingCompleteImplementation
    testImplementation.extendsFrom weavingCompleteImplementation
}

dependencies {
    implementation libs.aspectjrt
    compileOnly libs.aspectjweaver
    aspectjWeaver libs.aspectjweaver
}

def COMPILER_EXTRA_ARGS = [
        "-showWeaveInfo",
        "-verbose",
        "-Xlint:adviceDidNotMatch=error"
]

tasks.named('compileJava') {
    ajc.options.compilerArgs
            += COMPILER_EXTRA_ARGS
}

tasks.named('compileTestJava') {
    ajc.options.compilerArgs
            += COMPILER_EXTRA_ARGS
}

afterEvaluate {

    configurations.weavingCompleteImplementation.dependencies.forEach {

        var module = project(":" + it.name)
        var moduleJarTask = module.tasks.named("jar")

        def COMPILER_INPATH_ARG = [
                "-inpath", moduleJarTask.get().archiveFile.get().asFile.absolutePath
        ]

        tasks.named('compileJava') {
            dependsOn(moduleJarTask)
            ajc.options.compilerArgs
                    += COMPILER_INPATH_ARG
        }

        tasks.named('compileTestJava') {
            dependsOn(moduleJarTask)
            ajc.options.compilerArgs
                    += COMPILER_INPATH_ARG
        }

    }

}
